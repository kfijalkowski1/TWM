# call `just` to setup the environment - this operation is idempotent
default: setup

# prepare the environment
setup:
    #!/usr/bin/env -S bash -euo pipefail
    pdm install --with dev
    pdm run pre-commit install
    TOKEN=""
    read -s -p "Paste your wandb token: " TOKEN
    perl -pe "s/^(WANDB_TOKEN=).*$/\1\Q$TOKEN\E/" .env.sample > .env

    git submodule update --init --recursive
    touch twm/external/deeplab_v3plus/__init__.py
    touch twm/external/dehazeformer/__init__.py
    touch twm/external/deeplab_forked/__init__.py
    touch twm/external/dehazeformer_forked/__init__.py


# download cityscapes and foggy cityscapes datasets
get-data:
    #!/usr/bin/env -S bash -euo pipefail
    heading () {
        BLUE_BG=$'\033[44m'
        NC=$'\e[0m'
        echo "${BLUE_BG}$1${NC}"
    }

    mkdir -p data
    cd data
    USERNAME=""
    read -p "Enter CityScapes username: " USERNAME
    PASSWORD=""
    read -s -p "Enter CityScapes password: " PASSWORD
    mkdir -p cityscapes
    mkdir -p cityscapes_foggy
    mkdir -p temp

    heading "Fetching ground truth data..."
    wget --keep-session-cookies --save-cookies=temp/cookies.txt --post-data "username=$USERNAME&password=$PASSWORD&submit=Login" https://www.cityscapes-dataset.com/login/ -O /dev/null
    wget --load-cookies temp/cookies.txt --content-disposition https://www.cityscapes-dataset.com/file-handling/?packageID=1 -O temp/gtFine_trainvaltest.zip
    unzip temp/gtFine_trainvaltest.zip -d temp/gtFine_trainvaltest
    cp -r temp/gtFine_trainvaltest/gtFine cityscapes/gtFine
    cp -r temp/gtFine_trainvaltest/gtFine cityscapes_foggy/gtFine
    rm -rf temp/gtFine_trainvaltest/gtFine
    rm temp/gtFine_trainvaltest.zip

    heading "Fetching CityScapes..."
    wget --keep-session-cookies --save-cookies=temp/cookies.txt --post-data "username=$USERNAME&password=$PASSWORD&submit=Login" https://www.cityscapes-dataset.com/login/ -O /dev/null
    wget --load-cookies temp/cookies.txt --content-disposition https://www.cityscapes-dataset.com/file-handling/?packageID=3 -O temp/leftImg8bit_trainvaltest.zip
    unzip temp/leftImg8bit_trainvaltest.zip -d temp/cityscapes
    mv temp/cityscapes/leftImg8bit cityscapes/leftImg8bit
    rm temp/leftImg8bit_trainvaltest.zip

    heading "Fetching FoggyCityScapes..."
    wget --keep-session-cookies --save-cookies=temp/cookies.txt --post-data "username=$USERNAME&password=$PASSWORD&submit=Login" https://www.cityscapes-dataset.com/login/ -O /dev/null
    wget --load-cookies temp/cookies.txt --content-disposition https://www.cityscapes-dataset.com/file-handling/?packageID=29 -O temp/leftImg8bit_trainvaltest_foggy.zip
    unzip temp/leftImg8bit_trainvaltest_foggy.zip -d temp/foggy_cityscapes
    mv temp/foggy_cityscapes/leftImg8bit_foggy cityscapes_foggy/leftImg8bit
    rm temp/leftImg8bit_trainvaltest_foggy.zip

    heading "Cleaning up..."
    rm -rf temp
    heading "DONE!"

get-models:
    #!/usr/bin/env -S bash -euo pipefail
    heading () {
         BLUE_BG=$'\033[44m'
         NC=$'\e[0m'
         echo "${BLUE_BG}$1${NC}"
     }

    mkdir -p checkpoints

    heading "Fetching pretrained DeepLabV3Plus..."
    wget -O checkpoints/deeplabv3plus_mobilenet_cityscapes.pth "https://www.dropbox.com/scl/fo/i4k86qqiuzy33aawug287/AJZbFCPIvT521xW8lekSDr8/best_deeplabv3plus_mobilenet_cityscapes_os16.pth?rlkey=fgm7rgpeankeh9394j492sxif&dl=1"

    heading "Fetching pretrained DehazeFormer..."
    gdown 1bjSzDGGCjdFB-ixEDPDMm8bmke1PBtkp -O checkpoints/dehazeformer-t.pth
